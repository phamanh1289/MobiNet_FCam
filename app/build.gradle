import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    androidExtensions {
        experimental = true
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "vn.com.fpt.mobinet_fcam"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 33
        versionName  "2.3.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            storeFile file(properties.getProperty('storeFile'))
            keyAlias properties.getProperty('keyAlias')
            storePassword properties.getProperty('storePassword')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            configApkFileName(variant, defaultConfig)
        }
        staging {
            matchingFallbacks = ['debug', 'qa', 'release']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:multidex:1.0.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    //Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.4.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.4.0"
    implementation "com.squareup.retrofit2:converter-gson:2.4.0"
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    // Dagger 2
    implementation 'com.google.dagger:dagger:2.14.1'
    kapt 'com.google.dagger:dagger-compiler:2.14.1'
    //Rx
    implementation "io.reactivex.rxjava2:rxjava:2.1.16"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation 'com.tuyenmonkey:mkloader:1.4.0'
    //Permission
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    //Fonts
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    //Zoom image
    implementation 'it.sephiroth.android.library.imagezoom:imagezoom:2.3.0'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
}

static def getCurrentDayTime() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("yyyyMMdd")
    return df.format(today)
}

def configApkFileName(variant, defaultConfig) {
    variant.outputs.each { output ->
        if (variant.buildType.name == android.buildTypes.release.name) {
            output.outputFileName = new File("", "MobiQC_" + variant.buildType.name
                    + "_v" + defaultConfig.versionName + "_"
                    + defaultConfig.versionCode + "_"
                    + getCurrentDayTime()
                    + ".apk")
        }
    }
}
